@pub class StringBuffer() {
  var buf: Array[UInt8] = Array[UInt8]::empty();
  var len: Int64 = 0L;

  @pub fun size(): Int64 {
    return self.len;
  }

  @pub fun capacity(): Int64 {
    return self.buf.size();
  }

  // reserve `elements` bytes
  // (capacity - length >= elements)
  @pub fun reserve(elements: Int64) {
    if self.capacity() - self.size() >= elements {
      return;
    }

    let newcap = self.newCapacity(elements);
    let newbuf = Array[UInt8]::zero(newcap);
    var i = 0L;

    while i < self.buf.size() {
      newbuf.set(i, self.buf.get(i));
      i = i + 1L;
    }

    self.buf = newbuf;
  }

  @pub fun newCapacity(reserve: Int64): Int64 {
    var len = self.len;

    if len == 0L {
      len = 4L;
    }

    let c1 = (len + reserve + 7L) & !7L;
    let c2 = len * 2L;

    if c1 > c2 {
      c1
    } else if c2 - c1 > 32L {
      c1
    } else {
      c2
    }
  }

  @pub fun appendChar(ch: Char): StringBuffer {
    let chLen = ch.lenUtf8();
    self.reserve(chLen.toInt64());
    ch.encodeUtf8(self.buf, self.len);
    self.len = self.len + chLen.toInt64();
    self
  }

  @pub fun append(value: String): StringBuffer {
    self.reserve(value.size());
    var i = 0L;

    while i < value.size() {
      self.buf.set(self.len + i, value.getByte(i));
      i = i + 1L;
    }

    self.len = self.len + value.size();
    self
  }

  @pub fun appendNewline(): StringBuffer {
    self.appendChar('\n')
  }

  @pub fun toString(): String = String::fromBytesPart(self.buf, 0L, self.size()).getOrPanic();

  @pub @static fun empty(): StringBuffer = StringBuffer();
}

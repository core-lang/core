class Node(var left: Option[Node], var right: Option[Node]) {
    var i: Int;
    var j: Int;

    @static fun leaf() -> Node = Node(none[Node](), none[Node]());
}

var stretchTreeDepth: Int;
var longLivedTreeDepth: Int;
const kArraySize: Int = 500000;
const kMinTreeDepth: Int = 4;
const kMaxTreeDepth: Int = 16;

fun treeSize (i: Int) -> Int {
    return (1 << (i + 1)) - 1;
}

fun numIters(i: Int) -> Int {
    return 4 * treeSize(stretchTreeDepth) / treeSize(i);
}

fun populate(var iDepth: Int, thisNode: Node) {
    if iDepth <= 0 {
        return;
    } else {
        iDepth = iDepth - 1;
        thisNode.left = some[Node](Node::leaf());
        thisNode.right = some[Node](Node::leaf());
        populate(iDepth, thisNode.left.value);
        populate(iDepth, thisNode.right.value);
    }
}

fun makeTree(iDepth: Int) -> Node {
    if iDepth <= 0 {
        return Node::leaf();
    } else {
        return Node(some[Node](makeTree(iDepth-1)), some[Node](makeTree(iDepth-1)));
    }
}

fun timeConstruction(depth: Int) {
    let iNumIters = numIters(depth);
    println("Creating " + iNumIters.toString() + " trees of depth " + depth.toString());
    let start = timestamp();

    var i = 0;

    while i < iNumIters {
        let tempTree = Node::leaf();
        populate(depth, tempTree);
        i = i + 1;
    }

    let finish = timestamp();
    let msecs = (finish - start).toFloat() / 1000.0F / 1000.0F;
    println("\tTop down construction took " + msecs.toString() + "ms");

    let start = finish;

    var i = 0;

    while i < iNumIters {
        let tempTree = makeTree(depth);
        i = i + 1;
    }

    let finish = timestamp();
    let msecs = (finish - start).toFloat() / 1000.0F / 1000.0F;
    println("\tBottom up construction took " + msecs.toString() + "ms");
}

fun main() {
    if argc() != 1 {
        println("usage: gcbench <depth>");
        exit(1);
    }

    let depth = argv(0).parseInt();

    stretchTreeDepth = depth+1;
    longLivedTreeDepth = depth;

    println("Stretching memory with a binary tree of depth " + stretchTreeDepth.toString());
    let start = timestamp();

    makeTree(stretchTreeDepth);

    println("Creating a long-lived binary tree of depth " + longLivedTreeDepth.toString());
    let longLivedTree = Node::leaf();
    populate(longLivedTreeDepth, longLivedTree);

    println("Creating a long-lived array of " + kArraySize.toString() + " doubles");
    let array = arrayFill[Double](kArraySize, 0.0);

    var i = 0;

    while i < kArraySize / 2 {
        array(i) = 1.0/i.toDouble();
        i = i + 1;
    }

    var d = kMinTreeDepth;

    while d <= depth {
        timeConstruction(d);
        d = d + 2;
    }

    assert(array(1000) == 1.0/1000.0);
    let finish = timestamp();
    let elapsed = (finish - start).toFloat() / 1000.0F / 1000.0F;
    println("Completed in " + elapsed.toString() + "ms.");
}

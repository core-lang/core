fun main() {
  var n = 7;
  if argc() > 0 { n = argv(0).parseInt(); }
  println("Pfannkuchen ("+n.toString()+") = " + fannkuch(n).toString());
}

fun fannkuch(n: Int) -> Int {
  let perm = arrayFill[Int](n, 0);
  let perm1 = arrayFill[Int](n, 0);
  let count = arrayFill[Int](n, 0);
  var maxFlipsCount = 0;
  var permCount = 0;
  var checksum = 0;

  var i = 0;
  while i < n {
    perm1.set(i, i);
    i = i + 1;
  }

  var r = n;

  while true {
    while r != 1 {
      count.set(r-1, r);
      r = r - 1;
    }

    var i = 0;
    while i < n {
      perm.set(i, perm1.get(i));
      i = i + 1;
    }

    var flipsCount = 0;
    var k = perm.get(0);

    while k != 0 {
      let k2 = (k+1) / 2;

      var i = 0;
      while i < k2 {
        let temp = perm.get(i);
        perm.set(i, perm.get(k-i));
        perm.set(k-i, temp);

        i = i + 1;
      }

      flipsCount = flipsCount + 1;
      k = perm.get(0);
    }

    maxFlipsCount = max(maxFlipsCount, flipsCount);

    if permCount % 2 == 0 {
      checksum = checksum + flipsCount;
    } else {
      checksum = checksum - flipsCount;
    }

    while true {
      if r == n {
        println(checksum.toString());
        return maxFlipsCount;
      }

      let perm0 = perm1.get(0);
      var i = 0;

      while i < r {
        let j = i + 1;
        perm1.set(i, perm1.get(j));
        i = j;
      }

      perm1.set(r, perm0);

      count.set(r, count.get(r) - 1);

      if count.get(r) > 0 { break; }

      r = r + 1;
    }

    permCount = permCount + 1;
  }

  return -1;
}

fun max(a: Int, b: Int) -> Int {
  if a < b {
    return b;
  } else {
    return a;
  }
}

//= stdout file

fun main() {
    let primes = sieve(100i64);
    let mut i = 0i64;

    while i < primes.size() {
        println(primes(i).toString());
        i = i + 1i64;
    }
}

fun sieve(limit: Int64): Vec[Int64] {
    let primes = Vec[Int64]::new();

    if limit >= 2i64 {
        let numbers = Array[Bool]::fill(limit+1i64, true);
        let sqrtLimit = limit.toFloat64().sqrt().toInt64();

        let mut factor = 2i64;

        while factor <= sqrtLimit {
            if numbers(factor) {
                let mut multiple = factor * factor;

                while multiple <= limit {
                    numbers(multiple) = false;
                    multiple = multiple + factor;
                }
            }

            factor = factor + 1i64;
        }

        let mut i = 0i64;

        while i <= limit {
            if i >= 2i64 && numbers(i) {
                primes.push(i);
            }

            i = i + 1i64;
        }
    }

    return primes;
}